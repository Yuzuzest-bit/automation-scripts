#!/usr/bin/env bash
# make_tag_structure_dashboard.sh
#
# ç›®çš„:
# - æœªã‚¯ãƒ­ãƒ¼ã‚ºã‚¿ã‚¹ã‚¯ï¼ˆ+ãã®ç¥–å…ˆãƒãƒ¼ãƒˆï¼‰ã‚’ parent: ã«åŸºã¥ã„ã¦ãƒ„ãƒªãƒ¼è¡¨ç¤ºã™ã‚‹ï¼ˆé€±æ¬¡æ•´ç†ç”¨ï¼‰
#
# å‡ºåŠ›:
# - dashboards/structure_dashboard.md
#
# ä½¿ã„æ–¹:
#   1) ãƒ«ãƒ¼ãƒˆã ã‘æŒ‡å®šï¼ˆã‚¿ã‚°ç„¡ã—ï¼‰:
#      ./make_tag_structure_dashboard.sh /path/to/ROOT
#
#   2) ã‚¿ã‚°ã ã‘ï¼ˆROOTã¯PWDï¼‰:
#      ./make_tag_structure_dashboard.sh "issue"
#
#   3) ã‚¿ã‚° + ROOT:
#      ./make_tag_structure_dashboard.sh issue /path/to/ROOT
#      ./make_tag_structure_dashboard.sh issue ctx-life /path/to/ROOT
#
#   4) æ—§å½¢å¼äº’æ›:
#      ./make_tag_structure_dashboard.sh "issue ctx-life" "ignored" /path/to/ROOT
#
# ã‚ªãƒ—ã‚·ãƒ§ãƒ³ç’°å¢ƒå¤‰æ•°:
#   OPEN_DASH=0  â€¦ ç”Ÿæˆå¾Œã«è‡ªå‹•ã§é–‹ã‹ãªã„ï¼ˆãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã¯é–‹ãï¼‰
#   DEBUG=1      â€¦ ä¸€æ™‚ãƒ•ã‚¡ã‚¤ãƒ«ã‚’æ¶ˆã•ãšã€ä»¶æ•°ã‚’è¡¨ç¤º

set -euo pipefail

OPEN_DASH="${OPEN_DASH:-1}"
DEBUG="${DEBUG:-0}"

# ------------------------------------------------------------
# å¼•æ•°ãƒ‘ãƒ¼ã‚¹
# ------------------------------------------------------------
TAG_ARGS=()

if [ "$#" -eq 1 ] && [ -d "${1}" ]; then
  ROOT="${1}"
else
  if [ "$#" -eq 0 ]; then
    ROOT="$PWD"
  elif [ "$#" -eq 1 ]; then
    ROOT="$PWD"
    TAG_ARGS+=("$1")
  else
    eval "ROOT=\${$#}"
    i=1
    last=$(( $# - 1 ))
    while [ "$i" -le "$last" ]; do
      eval "arg=\${$i}"

      if [ "$i" -eq 1 ] && [ "$#" -ge 3 ] && [ "${2-}" = "ignored" ]; then
        for t in $arg; do
          [ -n "$t" ] && TAG_ARGS+=("$t")
        done
        break
      fi

      [ -n "$arg" ] && TAG_ARGS+=("$arg")
      i=$(( i + 1 ))
    done
  fi
fi

if [ "${#TAG_ARGS[@]}" -eq 0 ]; then
  TAG=""
else
  TAG="${TAG_ARGS[*]}"
fi

OUTDIR="${ROOT}/dashboards"
mkdir -p "${OUTDIR}"
OUT="${OUTDIR}/structure_dashboard.md"

tmp_nodes="$(mktemp)"
tmp_show="$(mktemp)"
tmp_edges="$(mktemp)"
tmp_edges_sorted="$(mktemp)"
filelist="$(mktemp)"

cleanup() {
  if [ "${DEBUG}" = "1" ]; then
    echo "[DBG] tmp_nodes=${tmp_nodes}"
    echo "[DBG] tmp_show=${tmp_show}"
    echo "[DBG] tmp_edges=${tmp_edges}"
    echo "[DBG] tmp_edges_sorted=${tmp_edges_sorted}"
    echo "[DBG] filelist=${filelist}"
  else
    rm -f "$tmp_nodes" "$tmp_show" "$tmp_edges" "$tmp_edges_sorted" "$filelist"
  fi
}
trap cleanup EXIT

# ------------------------------------------------------------
# å¯¾è±¡ markdown åˆ—æŒ™ï¼ˆdashboards/.dashboardignore ã‚’åæ˜ ï¼‰
# ------------------------------------------------------------
EXCLUDE_DIRS=()

# dashboards/.dashboardignoreï¼ˆ1è¡Œ=1é …ç›®, #ã‚³ãƒ¡ãƒ³ãƒˆå¯ï¼‰
IGNORE_FILE="${OUTDIR}/.dashboardignore"
if [ -f "${IGNORE_FILE}" ]; then
  while IFS= read -r line; do
    # ã‚³ãƒ¡ãƒ³ãƒˆé™¤å»ï¼‹å‰å¾Œç©ºç™½ãƒˆãƒªãƒ 
    line="$(printf '%s' "$line" | sed -e 's/[[:space:]]*#.*$//' -e 's/^[[:space:]]*//' -e 's/[[:space:]]*$//')"
    [ -n "$line" ] && EXCLUDE_DIRS+=("$line")
  done < "${IGNORE_FILE}"
fi

# prune å¯¾è±¡ï¼ˆãƒ‡ãƒ•ã‚©ãƒ«ãƒˆé™¤å¤–ï¼‰
PRUNE_DIRS=(
  "${OUTDIR}"
  "${ROOT}/.foam"
  "${ROOT}/.git"
  "${ROOT}/.vscode"
  "${ROOT}/node_modules"
)

# .dashboardignore ã®é™¤å¤–ï¼ˆROOTç›¸å¯¾ or çµ¶å¯¾ï¼‰
for ex in "${EXCLUDE_DIRS[@]}"; do
  ex="${ex%/}"
  [ -z "$ex" ] && continue

  # "templates/*" ã¿ãŸã„ãªæ›¸ãæ–¹ã‚‚è¨±å®¹ï¼ˆè¦ªãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã‚’é™¤å¤–ï¼‰
  if [[ "$ex" == */\* ]]; then
    ex="${ex%/*}"
    ex="${ex%/}"
    [ -z "$ex" ] && continue
  fi

  if [[ "$ex" == /* ]]; then
    PRUNE_DIRS+=("$ex")
  else
    ex="${ex#./}"
    PRUNE_DIRS+=("${ROOT}/${ex}")
  fi
done

# findï¼ˆ-prune ã§é™¤å¤–ï¼‰
FIND_CMD=(find "${ROOT}")
FIND_CMD+=("(" -type d "(")
first=1
for p in "${PRUNE_DIRS[@]}"; do
  if [ $first -eq 1 ]; then
    FIND_CMD+=(-path "$p")
    first=0
  else
    FIND_CMD+=(-o -path "$p")
  fi
done
FIND_CMD+=(")" -prune ")")
FIND_CMD+=(-o "(" -type f -name '*.md' -print ")")

"${FIND_CMD[@]}" > "${filelist}"

if [ "${DEBUG}" = "1" ]; then
  echo "[DBG] md files: $(wc -l < "${filelist}" | tr -d " ")"
  if [ -f "${IGNORE_FILE}" ]; then
    echo "[DBG] ignore file: ${IGNORE_FILE}"
    echo "[DBG] ignore dirs: ${EXCLUDE_DIRS[*]:-(none)}"
  else
    echo "[DBG] ignore file: (none)"
  fi
fi


# ------------------------------------------------------------
# Stage1: frontmatteræŠ½å‡º â†’ tmp_nodes
# å‡ºåŠ›TSV:
# id, parent, closed(0/1), due, pri, bd, gate, src, wgt, focus, awaiting, awaitingWho, basename, tagOK, relpath
#   - frontmatterç„¡ã—ã§ã‚‚å‡ºåŠ›ã™ã‚‹ï¼ˆclosed=0æ‰±ã„ï¼‰
#   - @focus / @awaiting ã¯æœ¬æ–‡ã‚‚å«ã‚ã¦å…¨æ–‡æ¤œç´¢ï¼ˆawkã®matchç¬¬3å¼•æ•°ã¯ä½¿ã‚ãªã„ï¼BSD awkäº’æ›ï¼‰
# ------------------------------------------------------------
awk -v tag="${TAG}" -v root="${ROOT}" '
function ltrim(s){ sub(/^[ \t\r\n]+/, "", s); return s }
function rtrim(s){ sub(/[ \t\r\n]+$/, "", s); return s }
function trim(s){ return rtrim(ltrim(s)) }

function strip_bom(s){
  sub(/^\357\273\277/, "", s)  # UTF-8 BOM
  return s
}
function relpath(p,    pre){
  pre = root
  if (substr(pre, length(pre), 1) != "/") pre = pre "/"
  if (index(p, pre) == 1) return substr(p, length(pre)+1)
  return p
}
function norm_tag(t,    s){
  s = tolower(trim(t))
  gsub(/^#+/, "", s)
  gsub(/^["'\''`]+|["'\''`]+$/, "", s)
  return s
}
function see_tag(t,    nt){
  nt = norm_tag(t)
  if (nt=="") return
  tagsSeen[nt]=1
  if (index(nt, "braindump")>0) isBrainDump=1
  if (nt ~ /^gate-/) isGate=1
}

BEGIN{
  nTag = 0
  if (tag != "") {
    nTag = split(tag, wantedRaw, /[[:space:]]+/)
    for (i=1; i<=nTag; i++) wanted[i] = norm_tag(wantedRaw[i])
  }
}

NR==FNR {
  file=$0
  gsub(/\r$/, "", file)
  if (file=="") next

  delete tagsSeen

  # defaults
  idVal=""; parentVal=""
  hasDue=0; dueVal=""
  isClosed=0
  isBrainDump=0
  isGate=0
  priVal=3
  srcVal="self"
  wgtVal="soft"

  isFocus=0
  isAwait=0
  awaitWho=""

  tagOK = (tag=="" ? 1 : 0)

  rp = relpath(file)

  # basename
  n = split(file, parts, "/")
  b = parts[n]
  if (length(b) > 3 && substr(b, length(b)-2) == ".md") b = substr(b, 1, length(b)-3)
  basename=b

  # frontmatter: æœ€åˆã®éç©ºè¡ŒãŒ --- ãªã‚‰FM
  inFM=0; hasFM=0; fmDone=0; inTags=0; firstNonEmptySeen=0

  while ((getline line < file) > 0) {
    sub(/\r$/, "", line)
    line = strip_bom(line)

    # --- æœ¬æ–‡æ¤œç´¢: @focus / @awaitingï¼ˆFMã®å¤–ã§ã‚‚ä¸­ã§ã‚‚OKï¼‰
    lowline = tolower(line)

    if (!isFocus) {
      if (match(lowline, /@focus([^[:alnum:]_]|$)/)) isFocus=1
    }
    if (!isAwait) {
      if (match(lowline, /@awaiting[[:space:]]+/)) {
        isAwait=1
        who = substr(line, RSTART+RLENGTH)
        who = trim(who)
        gsub(/\t+/, " ", who)
        gsub(/[[:space:]]+$/, "", who)
        # æœ«å°¾ã®ã‚³ãƒ¡ãƒ³ãƒˆã£ã½ã„ã‚‚ã®ã¯è»½ãå‰Šã‚‹ï¼ˆå¿…è¦ãªã‚‰ã“ã“ã‚’å¤–ã—ã¦ã‚‚OKï¼‰
        sub(/[[:space:]]*<!--.*$/, "", who)
        sub(/[[:space:]]*#.*$/, "", who)
        who = trim(who)
        if (length(who) > 60) who = substr(who, 1, 60) "..."
        awaitWho = who
      }
    }

    tmp=line; gsub(/[ \t]/, "", tmp)

    if (!firstNonEmptySeen) {
      if (tmp=="") continue
      firstNonEmptySeen=1
      if (line ~ /^[[:space:]]*---[[:space:]]*$/) {
        hasFM=1
        inFM=1
        continue
      } else {
        # frontmatterç„¡ã— â†’ FMè§£æã¯ã—ãªã„ãŒã€æœ¬æ–‡æ¤œç´¢ã¯ç¶šã‘ã‚‹
        hasFM=0
        inFM=0
        # ã“ã“ã‹ã‚‰å…ˆã‚‚èª­ã‚€ï¼ˆ@focus/@awaitingæ¤œå‡ºã®ãŸã‚ï¼‰
      }
    }

    if (hasFM && inFM==1) {
      low = tolower(line)
      copy = low; gsub(/[ \t]/, "", copy)

      # çµ‚ç«¯: --- ã¾ãŸã¯ ... ã‚’è¨±å¯ï¼ˆå£Šã‚ŒãŸFMã§æœ¬æ–‡ã¾ã§èª­ã¾ãªã„ï¼‰
      if (line ~ /^[[:space:]]*(---|\.\.\.)[[:space:]]*$/) {
        fmDone=1
        inFM=0
        inTags=0
        continue
      }

      # tags:ï¼ˆ1è¡Œ / ãƒªã‚¹ãƒˆä¸¡å¯¾å¿œï¼‰
      if (low ~ /^[[:space:]]*tags:[[:space:]]*/) {
        rest=line
        sub(/^[[:space:]]*tags:[[:space:]]*/, "", rest)
        rest = trim(rest)

        inTags = 0
        if (rest=="") {
          inTags = 1
        } else {
          gsub(/[\[\],]/, " ", rest)
          gsub(/["'\''`]/, "", rest)
          nt = split(rest, toks, /[[:space:]]+/)
          for (k=1; k<=nt; k++) see_tag(toks[k])
        }
      } else if (inTags==1) {
        if (line ~ /^[[:space:]]*-[[:space:]]*/) {
          item=line
          sub(/^[[:space:]]*-[[:space:]]*/, "", item)
          item = trim(item)
          gsub(/["'\''`]/, "", item)
          see_tag(item)
        } else if (tmp=="") {
          # ç©ºè¡Œã¯OK
        } else {
          inTags=0
        }
      }

      # id:
      if (low ~ /^[[:space:]]*id:[[:space:]]*/) {
        tmp2=line
        sub(/^[[:space:]]*id:[[:space:]]*/, "", tmp2)
        idVal=trim(tmp2)
        gsub(/^["'\''`]+|["'\''`]+$/, "", idVal)
      }

      # parent:
      if (low ~ /^[[:space:]]*parent:[[:space:]]*/) {
        tmp2=line
        sub(/^[[:space:]]*parent:[[:space:]]*/, "", tmp2)
        parentVal=trim(tmp2)
        gsub(/^["'\''`]+|["'\''`]+$/, "", parentVal)
      }

      # due:
      if (copy ~ /^due:/) {
        tmp2=line
        sub(/^[[:space:]]*due:[[:space:]]*/, "", tmp2)
        tmp2=trim(tmp2)
        if (tmp2 ~ /^[0-9]{4}-[0-9]{2}-[0-9]{2}/) {
          dueVal = substr(tmp2, 1, 10)
          hasDue = 1
        }
      }

      # closed:
      if (copy ~ /^closed:/) isClosed=1

      # priority:
      if (low ~ /^[[:space:]]*priority:[[:space:]]*/) {
        tmp2=line
        sub(/^[[:space:]]*priority:[[:space:]]*/, "", tmp2)
        tmp2=tolower(trim(tmp2))
        sub(/^#/, "", tmp2)
        tmp2=trim(tmp2)

        if (tmp2 ~ /^1/ || tmp2 ~ /^high/ || tmp2 ~ /^p1/) priVal=1
        else if (tmp2 ~ /^2/ || tmp2 ~ /^mid/ || tmp2 ~ /^medium/ || tmp2 ~ /^p2/) priVal=2
        else if (tmp2 ~ /^3/ || tmp2 ~ /^low/ || tmp2 ~ /^p3/) priVal=3
      }

      # due_source:
      if (low ~ /^[[:space:]]*due_source:[[:space:]]*/) {
        tmp2=tolower(line)
        sub(/^[[:space:]]*due_source:[[:space:]]*/, "", tmp2)
        tmp2=trim(tmp2)
        if (tmp2 ~ /^other/) srcVal="other"
        else srcVal="self"
      }

      # due_weight:
      if (low ~ /^[[:space:]]*due_weight:[[:space:]]*/) {
        tmp2=tolower(line)
        sub(/^[[:space:]]*due_weight:[[:space:]]*/, "", tmp2)
        tmp2=trim(tmp2)
        if (tmp2 ~ /^hard/) wgtVal="hard"
        else wgtVal="soft"
      }
    }
  }
  close(file)

  # ã‚¿ã‚°æ¡ä»¶è©•ä¾¡ï¼ˆtagsSeen ã«å…¨éƒ¨ã‚ã‚‹ã‹ï¼‰
  if (tag != "") {
    allOK=1
    for (ti=1; ti<=nTag; ti++) {
      wt = wanted[ti]
      if (wt=="") continue
      if (!(wt in tagsSeen)) { allOK=0; break }
    }
    if (allOK) tagOK=1
  }

  # @focus ãŒã‚ã‚Œã° @awaiting è¡¨ç¤ºã¯æŠ‘åˆ¶ï¼ˆå„ªå…ˆï¼‰
  if (isFocus==1) {
    isAwait=0
    awaitWho=""
  }

  # awaitingWho ã®TSVå®‰å…¨åŒ–
  gsub(/\t+/, " ", awaitWho)
  gsub(/\r+/, " ", awaitWho)
  gsub(/\n+/, " ", awaitWho)
  awaitWho = trim(awaitWho)

  printf("%s\t%s\t%d\t%s\t%d\t%d\t%d\t%s\t%s\t%d\t%d\t%s\t%s\t%d\t%s\n",
         idVal, parentVal, isClosed, (hasDue?dueVal:""), priVal, isBrainDump, isGate,
         srcVal, wgtVal, isFocus, isAwait, awaitWho, basename, tagOK, rp)
}' "${filelist}" > "${tmp_nodes}"

if [ "${DEBUG}" = "1" ]; then
  echo "[DBG] nodes lines: $(wc -l < "${tmp_nodes}" | tr -d " ")"
fi

# ------------------------------------------------------------
# Stage2: showé›†åˆï¼ˆæœªã‚¯ãƒ­ãƒ¼ã‚º + ç¥–å…ˆï¼‰ â†’ tmp_show
# tmp_show TSV:
# id, parent, key, openDesc, closed, due, pri, bd, gate, src, wgt, focus, awaiting, awaitingWho, base, active, relpath
# ------------------------------------------------------------
awk -F'\t' '
function ltrim(s){ sub(/^[ \t\r\n]+/, "", s); return s }
function rtrim(s){ sub(/[ \t\r\n]+$/, "", s); return s }
function trim(s){ return rtrim(ltrim(s)) }

function norm_ref(s,    t) {
  t = trim(s)
  gsub(/^["'\''`]+|["'\''`]+$/, "", t)

  if (t ~ /^\[\[/) {
    sub(/^\[\[/, "", t)
    sub(/\]\]$/, "", t)
    sub(/\|.*/, "", t)
    t = trim(t)
  }
  sub(/\.md$/, "", t)
  return t
}

BEGIN{ ROOT="ROOT" }

{
  id=$1
  parentRaw=$2
  closed=$3+0
  due=$4
  pri=$5+0
  bd=$6+0
  gate=$7+0
  src=$8
  wgt=$9
  focus=$10+0
  awaiting=$11+0
  awho=$12
  base=$13
  tagOK=$14+0
  rel=$15

  if (id=="") id="path:" rel

  ids[++N]=id
  baseById[id]=base
  relById[id]=rel

  idByBase[base]=id

  rawParent[id]=parentRaw
  closedById[id]=closed
  dueById[id]=due
  priById[id]=pri
  bdById[id]=bd
  gateById[id]=gate
  srcById[id]=src
  wgtById[id]=wgt
  focusById[id]=focus
  awaitingById[id]=awaiting
  awaitWhoById[id]=awho
  tagOKById[id]=tagOK
}

END{
  for (i=1; i<=N; i++) {
    id=ids[i]
    p = norm_ref(rawParent[id])

    if (p=="" || p=="-") { parentOf[id]=ROOT; continue }

    if (p in baseById)        parentOf[id]=p
    else if (p in idByBase)   parentOf[id]=idByBase[p]
    else                      parentOf[id]=ROOT
  }

  for (i=1; i<=N; i++) {
    id=ids[i]
    if (tagOKById[id]==1 && closedById[id]==0) active[id]=1
  }

  for (i=1; i<=N; i++) {
    id=ids[i]
    if (!active[id]) continue

    show[id]=1
    k = (dueById[id]!="") ? dueById[id] : "9999-99-98"

    cur=id
    guard=0
    while (cur!=ROOT && guard<200) {
      if (!(cur in keyById) || k < keyById[cur]) keyById[cur]=k

      p=parentOf[cur]
      if (p==ROOT) break

      show[p]=1
      openDesc[p]++
      if (p==cur) break
      cur=p
      guard++
    }

    if (!(id in openDesc)) openDesc[id]=0
  }

  for (i=1; i<=N; i++) {
    id=ids[i]
    if (!show[id]) continue

    p = parentOf[id]
    k = (id in keyById) ? keyById[id] : "9999-99-99"

    od = 0
    if (id in openDesc) od = openDesc[id]

    af = 0
    if (active[id]) af = 1

    print id "\t" p "\t" k "\t" od "\t" closedById[id] "\t" dueById[id] \
          "\t" priById[id] "\t" bdById[id] "\t" gateById[id] "\t" srcById[id] \
          "\t" wgtById[id] "\t" focusById[id] "\t" awaitingById[id] "\t" awaitWhoById[id] \
          "\t" baseById[id] "\t" af "\t" relById[id]
  }
}' "${tmp_nodes}" > "${tmp_show}"

if [ "${DEBUG}" = "1" ]; then
  echo "[DBG] show lines: $(wc -l < "${tmp_show}" | tr -d " ")"
fi

# ------------------------------------------------------------
# edgesä½œæˆï¼†ã‚½ãƒ¼ãƒˆï¼ˆparentã”ã¨ã«ã€é…ä¸‹æœ€çŸ­dueâ†’priorityâ†’id ã®é †ï¼‰
# ------------------------------------------------------------
awk -F'\t' '{ print $2 "\t" $3 "\t" $7 "\t" $1 }' "${tmp_show}" > "${tmp_edges}"
sort -k1,1 -k2,2 -k3,3n -k4,4 "${tmp_edges}" > "${tmp_edges_sorted}"

# ------------------------------------------------------------
# Stage3: Markdownå‡ºåŠ›ï¼ˆç©ºè¡Œãªã— / root-orphanåˆ†å‰²ãªã—ï¼‰
# ------------------------------------------------------------
{
  echo "# Structure â€“ è¦ªå­ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆï¼ˆé€±æ¬¡æ•´ç†ç”¨ï¼‰"
  echo "- ç”Ÿæˆæ™‚åˆ»: $(date '+%Y-%m-%d %H:%M')"
  if [ -z "${TAG}" ]; then
    echo "- æ¡ä»¶: æœªã‚¯ãƒ­ãƒ¼ã‚ºï¼ˆ+ç¥–å…ˆãƒãƒ¼ãƒˆã‚’è¡¨ç¤ºï¼‰"
  else
    echo "- æ¡ä»¶: tags ã«ã€Œ${TAG}ã€ã™ã¹ã¦ã‚’å«ã‚€æœªã‚¯ãƒ­ãƒ¼ã‚ºï¼ˆ+ç¥–å…ˆãƒãƒ¼ãƒˆã‚’è¡¨ç¤ºï¼‰"
  fi
  echo "- ä¸¦ã³: è¦ªãƒ–ãƒ­ãƒƒã‚¯ã¯ã€Œé…ä¸‹ã®æœ€çŸ­ dueã€ã§æ¦‚ã­å‰ã¸ã€å­ã¯ãã®ä¸­ã§åŒæ§˜ã«ä¸¦ã¶ï¼ˆå®Œå…¨ãªå…¨ä½“ã‚½ãƒ¼ãƒˆã¯ã—ãªã„ï¼‰"
  echo "- è¨˜å·: ğŸ”´ğŸŸ ğŸŸ¢ priority / ğŸš§ gate / ğŸ”¥ BrainDump / ğŸ¤ other / âš ï¸ hard / âœ… closed / âš ï¸âœ…ï¼é–‰ã˜ã¦ã‚‹ã®ã«æœªå®Œäº†ã®å­ãŒã„ã‚‹ç–‘ã„ / ğŸ¯ @focus / â³ @awaiting"
  echo "## ğŸ§­ ROOT"

  awk -F'\t' '
  function ltrim(s){ sub(/^[ \t\r\n]+/, "", s); return s }
  function rtrim(s){ sub(/[ \t\r\n]+$/, "", s); return s }
  function trim(s){ return rtrim(ltrim(s)) }

  function indent(n,    s,i){ s=""; for(i=0;i<n;i++) s=s"  "; return s }
  function pri_icon(p){
    if (p<=1) return "ğŸ”´"
    else if (p==2) return "ğŸŸ "
    else return "ğŸŸ¢"
  }
  function meta_icon(src,wgt,    s){
    s=""
    if (src=="other") s=s"ğŸ¤"
    if (wgt=="hard")  s=s"âš ï¸"
    return s
  }
  function combo_icon(pri, gate, bd,    s){
    s=pri_icon(pri)
    if (gate>0) s=("ğŸš§" s)
    if (bd>0)   s=("ğŸ”¥" s)
    return s
  }
  function wlink(id,    b,r){
    b=baseById[id]; r=relById[id]
    if (b=="") return "[[UNKNOWN]]"
    if (baseCount[b] > 1 && r!="") return "[[" r "|" b "]]"
    return "[[" b "]]"
  }
  function esc_inline(s){
    # ç®‡æ¡æ›¸ãã®ä¸­ã§å¤‰ã«å´©ã‚Œãªã„ç¨‹åº¦ã«ã‚¿ãƒ–ã ã‘æ½°ã™ï¼ˆå¿…è¦æœ€ä½é™ï¼‰
    gsub(/\t+/, " ", s)
    return s
  }

  NR==FNR{
    id=$1; parent=$2
    openDesc=$4+0
    closed=$5+0
    due=$6
    pri=$7+0
    bd=$8+0
    gate=$9+0
    src=$10
    wgt=$11
    focus=$12+0
    awaiting=$13+0
    awho=$14
    base=$15
    active=$16+0
    rel=$17

    id=trim(id)
    parent=trim(parent)

    ids[++N]=id

    parentOf[id]=parent
    closedById[id]=closed
    dueById[id]=due
    priById[id]=pri
    bdById[id]=bd
    gateById[id]=gate
    srcById[id]=src
    wgtById[id]=wgt
    focusById[id]=focus
    awaitingById[id]=awaiting
    awaitWhoById[id]=awho
    baseById[id]=base
    relById[id]=rel
    openDescById[id]=openDesc
    activeById[id]=active

    baseCount[base]++
    next
  }

  {
    p=trim($1)
    id=trim($4)
    if (p=="" || id=="") next
    if (children[p]=="") children[p]=id
    else children[p]=children[p] "\n" id
  }

  function print_node(id, depth,    line,mi,icon,title,mark,em,aw,lk){
    if (vis[id]) {
      print indent(depth) "- ğŸ” " wlink(id) " (cycle?)"
      return
    }
    vis[id]=1

    mi = meta_icon(srcById[id], wgtById[id])
    icon = combo_icon(priById[id], gateById[id], bdById[id])

    # emphasis marker (ğŸ¯ or â³)
    em=""
    if (focusById[id]==1) {
      em="ğŸ¯"
    } else if (awaitingById[id]==1) {
      aw = trim(awaitWhoById[id])
      aw = esc_inline(aw)
      if (aw!="") em="â³(" aw ")"
      else        em="â³"
    }

    lk = wlink(id)

    # closed mark & link formatting
    if (closedById[id]==1) {
      mark = "âœ…"
      if (openDescById[id] > 0) mark = "âš ï¸âœ…"
      lk = "~~" lk "~~"
    } else {
      mark = ""
    }

    # å¼·èª¿ï¼šğŸ¯/â³ ãŒä»˜ããƒãƒ¼ãƒˆã¯ãƒªãƒ³ã‚¯ã‚’å¤ªå­—ã«ã™ã‚‹
    if (em!="") lk = "**" lk "**"

    title = lk

    if (dueById[id] != "") line = "- " dueById[id] " " icon
    else                   line = "- " icon

    if (mark != "") line = line " " mark
    if (mi != "")   line = line " " mi
    if (em != "")   line = line " " em
    line = line " " title

    print indent(depth) line

    if (children[id] != "") {
      n = split(children[id], arr, "\n")
      for (i = 1; i <= n; i++) {
        cid = trim(arr[i])
        if (cid=="") continue
        print_node(cid, depth+1)
      }
    }
  }

  END{
    if (N==0) { print "> è©²å½“ãªã—"; exit }

    # 1) parent=ROOT ã®ã‚‚ã®ã‚’å…ˆã«å‡ºã™
    if (children["ROOT"]!="") {
      n = split(children["ROOT"], arr, "\n")
      for (i=1; i<=n; i++) {
        rid = trim(arr[i])
        if (rid=="") continue
        print_node(rid, 0)
      }
    }

    # 2) ROOTã«ç¹‹ãŒã‚‰ãªã„ãƒãƒ¼ãƒ‰ã‚‚ç¶šã‘ã¦å‡ºã™ï¼ˆã‚»ã‚¯ã‚·ãƒ§ãƒ³åˆ†ã‘ç„¡ã—ï¼‰
    for (i=1; i<=N; i++) {
      id = ids[i]
      if (id=="" || id=="ROOT") continue
      if (vis[id]) continue
      print_node(id, 0)
    }
  }
  ' "${tmp_show}" "${tmp_edges_sorted}"

} > "${OUT}"

echo "[INFO] Wrote ${OUT}"

# ------------------------------------------------------------
# ç”Ÿæˆå¾Œã«ãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰ã‚’é–‹ã
# ------------------------------------------------------------
if [ "${OPEN_DASH}" != "0" ]; then
  if command -v code >/dev/null 2>&1; then
    code -r "${OUT}" >/dev/null 2>&1 || true
  elif command -v open >/dev/null 2>&1; then
    open "${OUT}" >/dev/null 2>&1 || true
  elif command -v xdg-open >/dev/null 2>&1; then
    xdg-open "${OUT}" >/dev/null 2>&1 || true
  fi
fi
